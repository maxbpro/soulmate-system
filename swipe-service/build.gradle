plugins {
	id 'java-library'
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.4'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:2025.0.0"
		mavenBom("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom:2.23.0")
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(24)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

tasks.named('compileJava') {
	dependsOn 'printEnvVars'
}

tasks.register('printEnvVars') {
	doLast {
		println "GITHUB_ACTOR: ${System.getenv('GITHUB_ACTOR')}"
		println "PERSONAL_ACCESS_TOKEN: ${System.getenv('PERSONAL_ACCESS_TOKEN')}"
	}
}


repositories {
	mavenCentral()
	maven {
		name = "GitHubPackages"
		url = uri("https://maven.pkg.github.com/maxbpro/soulmate-system")
		credentials {
			username = System.getenv("GITHUB_ACTOR") ?: project.properties["gpr.user"]
			password = System.getenv("PERSONAL_ACCESS_TOKEN") ?: project.properties["gpr.key"]
		}
	}
	maven {
		url 'https://packages.confluent.io/maven/'
	}
}

dependencies {

	// Internal dependencies
	implementation 'ru.maxb.soulmate.common:common:1.0.46'

	// Spring Boot Starters
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-aop'


	// Data
	implementation 'org.springframework.data:spring-data-cassandra'

	// Monitoring & Observability
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'io.micrometer:micrometer-observation'
	implementation 'io.micrometer:micrometer-tracing-bridge-otel'
	implementation "io.opentelemetry:opentelemetry-exporter-otlp"
	implementation "io.opentelemetry.instrumentation:opentelemetry-spring-boot-starter:${opentelemetrySpringBootVersion}"

	// Resilience
	implementation "io.github.resilience4j:resilience4j-spring-boot3:${resilience4jSpringBoot3Version}"

	// Utilities
	implementation "net.logstash.logback:logstash-logback-encoder:$logstashVersion"
	compileOnly 'org.projectlombok:lombok'
	compileOnly "org.mapstruct:mapstruct:${mapstructVersion}"

	// Annotation Processors
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

	// Test Dependencies
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

	// Testcontainers
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:cassandra'

	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
	useJUnitPlatform()
	include '**/*'
}